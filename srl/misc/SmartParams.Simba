Procedure FixSmartSpeed;
var
  T: Integer;
begin
  T:= GetSystemTime + 2000;
  While (Not SmartEnabled) Do
  begin
    if (GetSystemTime >= T) Then
      Break;
    Wait(500);
  end;

  If (GetColor(382, 252) = 0) then
  begin
    If RSReady Then
      Exit;
    SmartSetRefresh(SmartGetRefresh + 1);
    MMouse(4, 4, 0, 0);
    SmartSetRefresh(SmartGetRefresh - 1);
  end;
end;

function GetSmartParams: TStringArray;
var
  Params: TStringArray;
  Page: String;
begin
  Page := Between('<iframe id="game" src="', '"', GetPage('http://www.runescape.com/game.ws?j=1'));
  Params := Explode(',', Page);
  Result := Params;
end;

function GetSmartIDs: TIntegerArray;
var
  i, count: Integer;
begin
  count := SmartGetClients(False);
  SetLength(Result, count);
  if count > 0 then
    for i:= 0 to count-1 do
      Result[i] := SmartClientID(i);
end;

function PairToSmart(id: Integer): boolean;
var
  i, c: Integer;
  Ids: TIntegerArray;
begin
  Ids := GetSmartIDs;
  c := Length(Ids);
  if c > 0 then
    for i:= 0 to c-1 do
      if (Ids[i] = id) and SmartPairClient(id) then
      begin
        SetEIOSTarget('libsmartremote', ToStr(id));
        Writeln('Paired with SMART['+ToStr(id)+']');
        Result := True;
        Exit;
      end;
  Writeln('** SMART remote['+ ToStr(id) +'] does not exist **');
end;

function InitSmart(force_new: boolean): integer;
var
  i, c: Integer;
  Ids: TIntegerArray;
  Params: TStringArray;
begin
  if not force_new then
  begin
    Ids := GetSmartIDs;
    c := Length(Ids);
    if c > 0 then
      for i:= 0 to c-1 do
        if SmartPairClient(Ids[i]) then
        begin
          Result := Ids[i];
          SetEIOSTarget('libsmartremote', ToStr(Result));
          Writeln('Paired with SMART['+ToStr(Result)+']');
          Exit;
        end;
  end;
  Params := GetSmartParams;
  if (Length(Params) < 2) then Exit;
  Writeln('Loading SMART: ' + params[0] + ',' + params[1]);
  Result := SmartSpawnClient('./Plugins/', params[0], ',' + params[1], 765, 503,
    's', '', '', -1);
  if (Result > 0) then SetEIOSTarget('libsmartremote', ToStr(Result));
end;

procedure KillAllSmartRemotes;
var
  Ids: TIntegerArray;
  i: Integer;
begin
  Ids := GetSmartIDs;
  for i := High(Ids) downto 0 do
  begin
    SmartKillClient(Ids[i]);
    Writeln('Killed SMART['+ToStr(Ids[i])+']');
  end;
end;

procedure SetupSmart;
var
  id, t: Integer;
begin
  t := GetTimeRunning;
  id := InitSmart(False);
  if (id <= 0) then
  begin
    Writeln('** SMART failed to load **');
    TerminateScript;
  end else
    Writeln('SMART loaded in ' + ToStr(GetTimeRunning - t) + ' msec');
end;
