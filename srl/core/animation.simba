//-----------------------------------------------------------------//
//--               Scar Standard Resource Library                --//
//--               » Animation Routines                          --//
//-----------------------------------------------------------------//
// * function PixelShiftMulti(B: TBoxArray; T: Integer): TIntegerArray; // * by marpis
// * function PixelShift(B: TBox; Time: Integer): Integer;         // * by marpis
// * function AnimatingMulti(B: TBoxArray; Time, MinCount: Integer): TBooleanArray; // * by marpis
// * function Animating(B: TBox; Time, MinCount: Integer): Boolean; // * by marpis
// * function IsMoving: Boolean;                                   // * by marpis

{*******************************************************************************
function PixelShiftMulti(B: TBoxArray; T: Integer): TIntegerArray;
By: marpis
Description: Calculates how many pixels change in boxes B during time T.
Date: Nov. 29, 2009
*******************************************************************************}
function PixelShiftMulti(B: TBoxArray; T: Integer): TIntegerArray;
Var
  L, I: Integer;
  BMP, BMP2: TIntegerArray;
begin
  L := Length(B);
  SetLength(BMP, L);
  SetLength(BMP2, L);
  SetLength(Result, L);

  for I := 0 to (L - 1) do
    BMP[I] := BitmapFromClient(B[i].x1,B[i].y1,B[i].x2,B[i].y2);
  Wait(T);
  for I := 0 to (L - 1) do
    BMP2[I] := BitmapFromClient(B[i].x1,B[i].y1,B[i].x2,B[i].y2);
  for i := 0 to (L - 1) do
    result[i] := CalculatePixelShift(BMP[i],BMP2[i],IntToBox(0,0,(b[i].x2 - b[i].x1),(b[i].y2-b[i].y1)));
  for I := 0 to (L - 1) do
  begin
    FreeBitmap(BMP[I]);
    FreeBitmap(BMP2[I]);
  end;
end;

{*******************************************************************************
function PixelShift(B: TBox; Time: Integer): Integer;
By: marpis
Description: Calculates how many pixels change in box B during time T.
Date: Nov. 29, 2009
*******************************************************************************}
function PixelShift(B: TBox; Time: Integer): Integer;
var
  TIA: TIntegerArray;
begin
  TIA := PixelShiftMulti([B], Time);
  Result := TIA[0];
end;

{*******************************************************************************
function AveragePixelShift(b: TBox; waitPerLoop, maxTime: integer): integer;
By: Coh3n
Description: Gets the average pixel shift every 'waitPerLoop' ms over the time
             'maxTime'
Date: Mar. 31st, 2011
*******************************************************************************}
function AveragePixelShift(b: TBox; waitPerLoop, maxTime: integer): integer;
var
  t: integer;
  shifts: TIntegerArray;
begin
  t := (getSystemTime + maxTime);

  while (getSystemTime < t) do
  begin
    setLength(shifts, length(shifts)+1);
    shifts[high(shifts)] := pixelShift(b, waitPerLoop);
  end;

  result := averageTIA(shifts);
end;

{*******************************************************************************
function AnimatingMulti(B: TBoxArray; Time, MinCount: Integer): TBooleanArray;
By: marpis
Description: Finds constant animation in boxes B during time T. If PixelShift
             in box is smaller than MinCount, it's considered not animating.
Date: Nov. 29, 2009
*******************************************************************************}
function AnimatingMulti(B: TBoxArray; Time, MinCount: Integer): TBooleanArray;
var
  Shifts: TIntegerArray;
  TT, I, H: Integer;
begin
  TT := GetSystemTime + Time;
  H := High(B);
  SetLength(Result, H + 1);
  for I := 0 to H do
    Result[I] := True;
  while (GetSystemTime < TT) do
  begin
   Shifts := PixelShiftMulti(B, 50);
   for I := 0 to H do
     if (Shifts[I] <= MinCount) then
       Result[I] := False;
  end;
end;

{*******************************************************************************
function Animating(B: TBox; Time, MinCount: Integer): Boolean;
By: marpis
Description: Finds constant animation in box B during time T. If PixelShift
             in box B is smaller than MinCount, it's considered not animating.
Date: Nov. 29, 2009
*******************************************************************************}
function Animating(B: TBox; Time, MinCount: Integer): Boolean;
var
  TBA: TBooleanArray;
begin
  TBA := AnimatingMulti([B], Time, MinCount);
  Result := TBA[0];
end;

{*******************************************************************************
function IsMoving: Boolean;
By: marpis
Description: Results true if character is moving.
Date: Nov. 29, 2009
*******************************************************************************}
function IsMoving: Boolean;
begin
  Result := PixelShift(IntToBox(MMCX-30, MMCY-30, MMCX+30, MMCY+30), 500) > 200;
end;
